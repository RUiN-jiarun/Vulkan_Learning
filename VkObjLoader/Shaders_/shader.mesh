#version 450
#extension GL_NV_mesh_shader: require

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 126) out;


layout(binding = 0) uniform UniformBufferObject 
{
    mat4 model;
    mat4 view;
    mat4 proj;
} ubo;


struct Vertex
{
	vec4 Position;
	vec4 Color;
};

layout(binding = 1) readonly buffer Vertices
{
	Vertex vertices[];
}g_Vertex;

struct Meshlet
{
	uint vertices[64];
	uint indices[126 * 3]; // up to 42 triangles
	uint indexCount;
	uint vertexCount;
};

layout(binding = 2) readonly buffer Meshlets
{
	Meshlet meshlets[];
}g_Meshlet;

layout(location = 0) out vec3 fragColor[];



uint hash(uint a)
{
   a = (a+0x7ed55d16) + (a<<12);
   a = (a^0xc761c23c) ^ (a>>19);
   a = (a+0x165667b1) + (a<<5);
   a = (a+0xd3a2646c) ^ (a<<9);
   a = (a+0xfd7046c5) + (a<<3);
   a = (a^0xb55a4f09) ^ (a>>16);
   return a;
}

void main()
{
	uint mi = gl_WorkGroupID.x;
	uint ti = gl_LocalInvocationID.x;

	uint mhash = hash(mi);
	vec3 mcolor = vec3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;

	//===== Vertext Clip Position =====================
	for (uint i = ti; i < uint(g_Meshlet.meshlets[mi].vertexCount); i+=32)
	{
		uint vi = g_Meshlet.meshlets[mi].vertices[i];
		vec3 pos = vec3(g_Vertex.vertices[vi].Position.xy, 0.0);

		gl_MeshVerticesNV[i].gl_Position = ubo.proj * ubo.view * ubo.model * vec4(pos, 1.0);

		fragColor[i] = mcolor;
	}

	//===== Index =====================
	for (uint i = ti; i < uint(g_Meshlet.meshlets[mi].indexCount); i+=32)
	{
		gl_PrimitiveIndicesNV[i] = uint(g_Meshlet.meshlets[mi].indices[i]);
	}

	//===== Primitive Count =====================	
	if (ti == 0)
		gl_PrimitiveCountNV = uint(g_Meshlet.meshlets[mi].indexCount) / 3;
}